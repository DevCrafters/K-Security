import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

group 'cloud.swiftnode.kspam'
version '2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "com.github.johnrengelman.shadow"

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

sourceCompatibility = 1.7

repositories {
    /* Spigot Repo */
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    /* Bungeecord Repo */
    maven { url 'https://oss.sonatype.org/content/groups/public' }
    /* Metrics */
    maven { url 'http://repo.mcstats.org/content/repositories/public' }
    /* ProtocolLib */
    maven { url 'http://repo.dmulloy2.net/content/groups/public/' }
    mavenCentral()
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.11-R0.1-SNAPSHOT'
//    compile 'org.mcstats.bukkit:metrics:R8-SNAPSHOT'
    compile 'com.comphenix.protocol:ProtocolLib-API:4.1.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.version
    ]
}

shadowJar {
    classifier = ''
    dependencies {
        include dependency('org.mcstats.bukkit:metrics')
    }
    relocate 'org.mcstats', "cloud.swiftnode.kspam.metrics"
}

artifacts {
    archives shadowJar
}
